import org.jetbrains.dokka.DokkaConfiguration.Visibility
import org.jetbrains.dokka.gradle.DokkaTaskPartial

buildscript {
    ext {
        kotlin_version = '1.9.23'
        nav_version = '2.7.7'
        hilt_version = '2.51'
        junit_version = '4.13.2'
        dokka_version = '1.9.20'
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        classpath 'com.google.gms:google-services:4.4.1'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.9'
        classpath 'com.google.firebase:perf-plugin:1.4.2'
    }
}

plugins {
    id "org.jetbrains.dokka" version "$dokka_version"
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version" apply false
}

dependencies {
    dokkaPlugin 'me.khruslan.tierlistmaker:dokka:1.0.0'
}

subprojects {
    apply plugin: 'org.jetbrains.dokka'

    dependencies {
        dokkaPlugin 'me.khruslan.tierlistmaker:dokka:1.0.0'
    }

    tasks.withType(DokkaTaskPartial.class).configureEach {
        suppressInheritedMembers.set(true)
        dokkaSourceSets.configureEach {
            sourceRoots.from(
                    // A workaround for https://github.com/Kotlin/dokka/issues/2821
                    file('build/generated/data_binding_base_class_source_out/debug/out/me/khruslan/tierlistmaker/databinding/ActivityHomeBinding.java'),
                    file('build/generated/data_binding_base_class_source_out/debug/out/me/khruslan/tierlistmaker/databinding/ActivityTierListBinding.java'),
                    file('build/generated/data_binding_base_class_source_out/debug/out/me/khruslan/tierlistmaker/databinding/FragmentCollectionBinding.java'),
                    file('build/generated/data_binding_base_class_source_out/debug/out/me/khruslan/tierlistmaker/databinding/FragmentTierListBinding.java'),
                    file('build/generated/data_binding_base_class_source_out/debug/out/me/khruslan/tierlistmaker/databinding/GroupTierListBottomBarBinding.java'),
                    file('build/generated/data_binding_base_class_source_out/debug/out/me/khruslan/tierlistmaker/databinding/ItemTierBinding.java'),
                    file('build/generated/data_binding_base_class_source_out/debug/out/me/khruslan/tierlistmaker/databinding/ItemTierListPreviewBinding.java'),
                    file('build/generated/data_binding_base_class_source_out/debug/out/me/khruslan/tierlistmaker/databinding/ViewHintOverlayBinding.java')
            )
            includes.from('packages.md')
            documentedVisibilities.set([
                    Visibility.PUBLIC,
                    Visibility.PRIVATE,
                    Visibility.PROTECTED,
                    Visibility.INTERNAL,
                    Visibility.PACKAGE
            ])
            externalDocumentationLink {
                url.set(new URL('https://jakewharton.github.io/timber/docs/5.x/'))
                packageListUrl.set(new URL('https://jakewharton.github.io/timber/docs/5.x/timber/package-list'))
            }
            externalDocumentationLink {
                url.set(new URL('https://javax-inject.github.io/javax-inject/api/'))
            }
            externalDocumentationLink {
                url.set(new URL('https://kotlinlang.org/api/kotlinx.coroutines/'))
            }
        }
    }
}

tasks.dokkaGfmMultiModule {
    outputDirectory.set(file('docs'))
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}