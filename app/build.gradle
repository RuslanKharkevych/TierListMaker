plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
}

android {
    compileSdk 34
    namespace 'me.khruslan.tierlistmaker'

    defaultConfig {
        applicationId "me.khruslan.tierlistmaker"
        minSdkVersion 21
        targetSdk 34
        versionCode 1
        versionName "1.0.0"
        resourceConfigurations += ['en', 'ru', 'uk']
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            versionNameSuffix '-DEBUG'
            resValue("bool", "firebase_analytics_collection_deactivated", "true")
            resValue("bool", "firebase_crashlytics_collection_enabled", "false")
            resValue("bool", "firebase_performance_logcat_enabled", "true")
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue("bool", "firebase_analytics_collection_deactivated", "false")
            resValue("bool", "firebase_crashlytics_collection_enabled", "true")
            resValue("bool", "firebase_performance_logcat_enabled", "false")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {

    // AndroidX
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.2'
    implementation 'androidx.core:core-splashscreen:1.0.1'

    // Google
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:32.6.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-perf'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Image processing
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'com.github.Shouheng88:compressor:1.6.0'

    // Logging
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Dependency injection
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Storage
    implementation 'io.github.pilgr:paperdb:2.7.2'
    implementation 'androidx.preference:preference-ktx:1.2.1'

    // Live Event
    implementation 'com.github.hadilq:live-event:1.3.0'

    // Lint
    lintChecks project(':app:lint')

    // Unit Tests
    testImplementation "junit:junit:$junit_version"
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
    testImplementation 'io.mockk:mockk:1.13.8'
    testImplementation 'com.jraska.livedata:testing-ktx:1.3.0'
    testImplementation 'io.reactivex.rxjava2:rxjava:2.2.21' // Needed to mock Compressor
}